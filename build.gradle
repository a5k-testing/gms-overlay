// -*- coding: utf-8; mode: conf-javaprop -*-
// SPDX-FileCopyrightText: (c) 2022 ale5000
// SPDX-License-Identifier: GPL-3.0-or-later
// SPDX-FileType: SOURCE

gradleEnterprise.buildScan {
    termsOfServiceUrl 'https://gradle.com/terms-of-service'
    termsOfServiceAgree 'yes'
    publishAlwaysIf System.getenv('UPLOAD_BUILD_SCAN') != null
}

static short parseThreeDigitNumber(String numericString) {
    short number = Short.parseShort(numericString)
    if(number > 999)
        throw new InvalidUserDataException('Value out of range. Value:"' + numericString + '"')

    return number
}

static int getVersionCode(String versionName) {
    String[] parts = versionName.split('\\.')
    if(parts.length != 3)
        throw new InvalidUserDataException('Invalid versionName')

    return (parseThreeDigitNumber(parts[0]) * 1000000) + (parseThreeDigitNumber(parts[1]) * 1000) + parseThreeDigitNumber(parts[2])
}

buildscript {
    ext.ourVersionName = '1.0.0'
    ext.androidMinSdk = 21

    apply from: 'dependencies.gradle'

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${androidBuildGradleVersion}"
    }
    version = 'v' + ourVersionName
}

ext.ourVersionCode = getVersionCode(ext.ourVersionName)

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}
